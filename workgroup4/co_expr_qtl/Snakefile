#!/usr/bin/env python
import pandas as pd
import os
import glob

# Check required input arguments.
if config["inputs"]["singularity_image"] is None or not os.path.exists(config["inputs"]["singularity_image"]):
    logger.error("Critical, singularity_image does not exist.\n\nExiting.")
    exit("MissingsingularityImage")
if config["inputs"]["repo_dir"] is None or not os.path.exists(config["inputs"]["repo_dir"]):
    logger.error("Critical, repo_dir does not exist.\n\nExiting.")
    exit("MissingRepoDir")
if config["outputs"]["output_dir"] is None:
    logger.error("Critical, the output_dir cannot be empty.\n\nExiting.")
    exit("MissingOutputDir")

# Add trailing /.
if not config["outputs"]["output_dir"].endswith("/"):
    config["outputs"]["output_dir"] += "/"

# Create the output directory.
os.makedirs(config["outputs"]["output_dir"], exist_ok=True)

def print_wildcards(wildcards):
    out = []
    for key, value in wildcards.items():
        out.append(key + "=" + value)
    return ", ".join(out)

# def get_batch_number(wildcards):
#   logger.debug("get_pool_samples({})".format(print_wildcards(wildcards)))
#   chromosomes = config["settings"]["chromosomes"]
#   chr_batch = {}
#   for chr in chromosomes:
#     out_dir = checkpoints.create_batches.get(cell_type = wildcards.cell_type, chr = chr).output["data_dir"]
#     pattern = config["outputs"]["output_dir"] + f"chr{chr}-batch-*.txt"
#     matching_files = glob.glob(pattern)
#     chr_batch[chr] = len(matching_files)
#   return chr_batch

def get_batch_number(wildcards):
  logger.debug("get_pool_samples({})".format(print_wildcards(wildcards)))
  out_dir = checkpoints.create_batches.get(**wildcards)
  batches = glob_wildcards(os.path.join(out_dir, f"chr{wildcards.chr}" + "-batch-{n_batch}.txt")
  logger.debug("\tfound batches: '" + ", ".join(batches.n_batch) + "'")

  if len(batches.n_batch) == 0:
    logger.error("Critical, no batches found.")
    exit()
  
  return batches.n_batch

rule all:
  input: 
    merged_coeqtls = expand(config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/{cell_type}-TopEffects-allsignificantTriplets.txt", cell_type=config["settings"]["cell_type"])
    # merged_coeqtls = expand(config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/{cell_type}-TopEffects.txt", cell_type=config["settings"]["cell_type"])

rule get_sample_ids:
  input: config["inputs"]["co_expr_dir"] + "correlations/data/{cell_type}/chr{chr}/{cell_type}.chr.{chr}.corr.transpose.stripped.txt.gz"
  output: config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/sample_id.txt"
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["get_sample_ids_memory"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["get_sample_ids_memory"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["get_sample_ids_time"]]
  threads: config["get_sample_ids_threads"]
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["singularity_image"],
    script =  config["inputs"]["repo_dir"] + "scripts/get_sample_ids.py"
  log: config["outputs"]["output_dir"] + "log/{cell_type}/chr{chr}/get_sample_ids.{cell_type}.log"
  shell:
    """
    singularity exec --bind {params.bind} {params.sif} python {params.script} \
      --input {input} \
      --output {output} > {log} 2>&1
    """

rule create_linkfile:
  input: config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/sample_id.txt"
  output: config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/linkfile.txt"
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["create_linkfile_memory"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["create_linkfile_memory"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["create_linkfile_time"]]
  threads: config["create_linkfile_threads"]
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["singularity_image"],
    script =  config["inputs"]["repo_dir"] + "scripts/create_linkfile.py",
    wg0_file_directories = config["inputs"]["wg0_file_directories"]
  log: config["outputs"]["output_dir"] + "log/{cell_type}/chr{chr}/create_linkefile.{cell_type}.log"
  shell:
    """
    singularity exec --bind {params.bind} {params.sif} python {params.script} \
      --sample_ids {input} \
      --wg0_file_directories {params.wg0_file_directories}\
      --output {output} > {log} 2>&1
    """

rule get_significant_eqtls:
  input:
    top_effects = lambda wildcards: config["inputs"]["top_effects"][wildcards.cell_type]
  output: config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/{cell_type}-significant_eqtls.txt"
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["get_significant_eqtls_threads"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["get_significant_eqtls_threads"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["get_significant_eqtls_time"]]
  threads: config["get_significant_eqtls_threads"]
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["singularity_image"],
    script =  config["inputs"]["repo_dir"] + "scripts/determine_significant_eqtl_snp_gene_pairs.py"
  log: config["outputs"]["output_dir"] + "log/{cell_type}/get_significant_eqtls.{cell_type}.log"
  shell:
    """
    singularity exec --bind {params.bind} {params.sif} python {params.script} {input.top_effects} {output} > {log} 2>&1
    """

rule get_gene_pairs:
  input: config["inputs"]["co_expr_dir"] + "correlations/data/{cell_type}/chr{chr}/{cell_type}.chr.{chr}.corr.transpose.stripped.txt.gz"
  output: config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/genepairs.txt"
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["get_gene_pairs_memory"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["get_gene_pairs_memory"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["get_gene_pairs_time"]]
  threads: config["get_gene_pairs_threads"]
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["singularity_image"],
    script =  config["inputs"]["repo_dir"] + "scripts/get_gene_pairs.py",
    out = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/"
  log: config["outputs"]["output_dir"] + "log/{cell_type}/chr{chr}/get_gene_pairs.{cell_type}.chr.{chr}.log"
  shell:
    """
    singularity exec --bind {params.bind} {params.sif} {params.script} --input {input} --out {params.out} > {log} 2>&1
    """

rule create_snp_gene_pair_triplet:
  input: 
    significant_eqtls = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/{cell_type}-significant_eqtls.txt",
    gene_pairs = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/genepairs.txt"
  output: 
    triplet = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/snp_genepair_triplets_to_test.txt",
    triplet_group = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/snp_genepair_triplets_to_test_groups.txt"
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["create_snp_gene_pair_triplet_memory"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["create_snp_gene_pair_triplet_memory"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["create_snp_gene_pair_triplet_time"]]
  threads: config["create_snp_gene_pair_triplet_threads"]
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["singularity_image"],
    script =  config["inputs"]["repo_dir"] + "scripts/create_snp_genepair_triplet_list.py"
  log: config["outputs"]["output_dir"] + "log/{cell_type}/chr{chr}/create_snp_gene_pair_triplet.{cell_type}.chr.{chr}.log"
  shell:
    """
    singularity exec --bind {params.bind} {params.sif} python {params.script} \
      {input.significant_eqtls} \
      {wildcards.chr} \
      {input.gene_pairs} \
      {output.triplet} \
      {output.triplet_group} > {log} 2>&1
    """

rule create_annotation:
  input: 
    gene_pairs = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/genepairs.txt",
    gene_annotation = config["inputs"]["gene_annotation"]
  output: 
    annotation = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/annotation.txt.gz"
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["create_annotation_memory"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["create_annotation_memory"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["create_annotation_time"]]
  threads: config["create_annotation_threads"]
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["singularity_image"],
    script =  config["inputs"]["repo_dir"] + "scripts/createAnnotation.py"
  log: config["outputs"]["output_dir"] + "log/{cell_type}/chr{chr}/create_annotation.{cell_type}.chr.{chr}.log"
  shell:
    """
    singularity exec --bind {params.bind} {params.sif} python {params.script} \
      {input.gene_pairs} \
      {input.gene_annotation} \
      {output.annotation} > {log} 2>&1
    """

rule filter_genotype:
  input:
    top_effects = lambda wildcards: config["inputs"]["top_effects"][wildcards.cell_type],
    genotype = config["inputs"]["genotype"]
  output:
    filtered_genotype = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/{cell_type}.eqtlsnps.bgz.vcf.gz"
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["filter_genotype_memory"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["filter_genotype_memory"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["filter_genotype_time"]] 
  threads: config["filter_genotype_threads"]
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["singularity_image"],
    script_dir =  config["inputs"]["repo_dir"] + "scripts/",
    out = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/{cell_type}."
  log: config["outputs"]["output_dir"] + "log/{cell_type}/filter_genotype.{cell_type}.log"
  shell:
    """
    singularity exec --bind {params.bind} {params.sif} python {params.script_dir}filter_genotype.py \
      --top_effects {input.top_effects} \
      --genotype {input.genotype} \
      --output {params.out}eqtlsnps.vcf.gz > {log} 2>&1

    bash {params.script_dir}compress_vcf.sh {params.out}eqtlsnps.vcf.gz {output.filtered_genotype}
    """

checkpoint create_batches:
  input:
    correlation = config["inputs"]["co_expr_dir"] + "correlations/data/{cell_type}/chr{chr}/{cell_type}.chr.{chr}.corr.transpose.stripped.txt.gz",
    linkfile = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/linkfile.txt",
    genotype = config["inputs"]["genotype"],
    gene_pairs = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/genepairs.txt",
    annotation = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/annotation.txt.gz",
    triplet = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/snp_genepair_triplets_to_test.txt",
    triplet_group = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/snp_genepair_triplets_to_test_groups.txt"
  output:
    data_dir = directory(config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/chr{chr}/batches/"),
    done = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/chr{chr}/batches/chr{chr}-batches.done"
    # batches = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/chr{chr}/batches/chr{chr}-batch-{n}.txt
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["singularity_image"],
    script =  config["inputs"]["repo_dir"] + "scripts/createBatches.py",
    batch_prefix = "{cell_type}-chr{chr}",
    n_genes = 5000,
    job_template = config["inputs"]["repo_dir"] + "scripts/job_template.sh",
    out = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/chr{chr}/",
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["create_batches_memory"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["create_batches_memory"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["create_batches_time"]]
  threads: config["create_batches_threads"]
  log: config["outputs"]["output_dir"] + "log/{cell_type}/chr{chr}/create_batches.{cell_type}.log"
  shell:
    """
    singularity exec --bind {params.bind} {params.sif} python {params.script} \
    {wildcards.cell_type}-chr{wildcards.chr} \
    {input.correlation} \
    {input.linkfile} \
    {input.genotype} \
    {input.gene_pairs} \
    {input.annotation} \
    {input.triplet} \
    {input.triplet_group} \
    {params.job_template} \
    {params.n_genes} \
    {params.out} > {log} 2>&1
    singularity exec --bind {params.bind} {params.sif} touch {output.done}
    """

rule run_co_eqtl:
  input:
    batch = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/chr{chr}/batches/chr{chr}-batch-{n_batch}.txt",
    correlation = config["inputs"]["co_expr_dir"] + "correlations/data/{cell_type}/chr{chr}/{cell_type}.chr.{chr}.corr.transpose.stripped.txt.gz",
    linkfile = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/linkfile.txt",
    filtered_genotype = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/{cell_type}.eqtlsnps.bgz.vcf.gz",
    gene_pairs = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/genepairs.txt",
    annotation = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/annotation.txt.gz",
    triplet = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/snp_genepair_triplets_to_test.txt",
    triplet_group = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/snp_genepair_triplets_to_test_groups.txt"
  output:
    finished = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/chr{chr}/output/chr{chr}-batch-{n_batch}-TopEffects.finished",
    coeqtls = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/chr{chr}/output/chr{chr}-batch-{n_batch}-TopEffects.txt"
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["run_co_eqtl_memory"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["run_co_eqtl_memory"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["run_co_eqtl_time"]]
  threads: config["run_co_eqtl_threads"]
  log: config["outputs"]["output_dir"] + "log/{cell_type}/chr{chr}/run_co_eqtl.{cell_type}.chr.{chr}.batch.{n_batch}.log"
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["singularity_image"],
    memory = "3g",
    threads = 2,
    permutations = config["mbqtl_settings"]["permutations"],
    minobservations = config["mbqtl_settings"]["minobservations"],
    mingenotypecount = config["mbqtl_settings"]["mingenotypecount"],
    jar = config["inputs"]["repo_dir"] + "scripts/MbQTL-1.5.1-SNAPSHOT-jar-with-dependencies.jar",
    out = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/chr{chr}/output/chr{chr}-batch-{n_batch}"
  shell:
    """
    singularity exec --bind {params.bind} {params.sif} java -Xmx1500m \
      -Djava.util.concurrent.ForkJoinPool.common.parallelism={params.threads} \
      -Dmaximum.threads={params.threads} \
      -Dthread.pool.size={params.threads} \
      -jar {params.jar} \
      -m mbqtl \
      -a {input.annotation} \
      -e {input.correlation} \
      -g {input.linkfile} \
      -v {input.filtered_genotype} \
      --chr {wildcards.chr} \
      --perm {params.permutations} \
      --minobservations {params.minobservations} \
      -gl {input.batch} \
      -o {params.out} \
      --expgroups {input.triplet_group} \
      --mingenotypecount {params.mingenotypecount} \
      --fisherzmeta \
      -sgl {input.triplet} > {log} 2>&1
    """

rule merge_batches:
  input: 
    coeqtls = lambda wildcards: expand(config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/chr{chr}/output/chr{chr}-batch-{n_batch}-TopEffects.txt",n_batch = get_batch_number(wildcards),  allow_missing=True)
  output:
    merged_batches = temp(config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/chr{chr}/{cell_type}-chr{chr}-TopEffects.txt")
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["merge_batches_memory"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["merge_batches_memory"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["merge_batches_time"]]
  threads: config["merge_batches_threads"]
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["singularity_image"],
    script = config["inputs"]["repo_dir"] + "scripts/mergeBatches.py",
  log: config["outputs"]["output_dir"] + "log/{cell_type}/chr{chr}/{cell_type}.chr{chr}.merge_batches.log"
  shell:
    """
    singularity exec --bind {params.bind} {params.sif} python {params.script} \
      --input {input.coeqtls} \
      --output {output.merged_batches} > {log} 2>&1
    """

rule merge_coeqtls:
  input: 
    sample_ids = expand(config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/sample_id.txt", chr = config["settings"]["chromosomes"], allow_missing = True),
    linkfile = expand(config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/linkfile.txt",chr = config["settings"]["chromosomes"], allow_missing = True),
    gene_pairs = expand(config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/genepairs.txt",chr = config["settings"]["chromosomes"], allow_missing = True),
    triplet = expand(config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/snp_genepair_triplets_to_test.txt",chr = config["settings"]["chromosomes"], allow_missing = True),
    triplet_group = expand(config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/snp_genepair_triplets_to_test_groups.txt",chr = config["settings"]["chromosomes"], allow_missing = True),
    annotation = expand(config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/annotation.txt.gz",chr = config["settings"]["chromosomes"], allow_missing = True),
    batches = expand(config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/chr{chr}/batches/chr{chr}-batches.done",chr = config["settings"]["chromosomes"], allow_missing = True),
    merged_batches = expand(config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/chr{chr}/{cell_type}-chr{chr}-TopEffects.txt", chr = config["settings"]["chromosomes"], allow_missing = True)
  output:
    coeqtls = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/{cell_type}-TopEffects.txt"
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["merge_co_eqtls_memory"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["merge_co_eqtls_memory"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["merge_co_eqtls_time"]]
  threads: config["merge_co_eqtls_threads"]
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["singularity_image"],
    script = config["inputs"]["repo_dir"] + "scripts/mergeBatches.py",
  log: config["outputs"]["output_dir"] + "log/{cell_type}/{cell_type}.merge_co_eqtls.log"
  shell:
    """
    singularity exec --bind {params.bind} {params.sif} python {params.script} \
      --input {input.merged_batches} \
      --output {output.coeqtls} > {log} 2>&1
    """

rule multiple_testing_correction:
  input:
    coeqtls = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/{cell_type}-TopEffects.txt"
  output:
    qval = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/{cell_type}-TopEffectswithQval.txt",
    hist = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/plots/{cell_type}-TopEffects_hist.png",
    overview = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/plots/{cell_type}-TopEffects_overview.png"
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["multiple_testing_correction_memory"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["multiple_testing_correction_memory"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["multiple_testing_correction_time"]]
  threads: config["multiple_testing_correction_threads"]
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["mbqtl_singularity_image"],
    script = config["inputs"]["repo_dir"] + "scripts/multiple_testing_correction.R",
    out = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/{cell_type}-TopEffects",
    plot_out = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/plots/{cell_type}-TopEffects"
  log: config["outputs"]["output_dir"] + "log/{cell_type}/{cell_type}.multiple_testing_correction.log"
  shell:
    """
    singularity exec --bind {params.bind} {params.sif} Rscript {params.script} \
    --input {input.coeqtls} \
    --nom_pvalue MetaP \
    --n_tests NrTestedSNPs \
    --beta_adj_pvalue BetaAdjustedMetaP \
    --beta_dist_a BetaDistAlpha \
    --beta_dist_b BetaDistBeta \
    --bonf_pvalue_col BonfAdjustedMetaP \
    --bonf_bh_fdr_col BonfBHAdjustedMetaP \
    --bh_fdr_col bh_fdr \
    --qvalue_col qval \
    --nom_thresh_col PvalueNominalThreshold \
    --alpha 0.05 \
    --data_out {params.out} \
    --plot_out {params.plot_out} \
    --suffix 'withQval'
    """

rule get_significant_effects:
  input:
    coeqtl = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/{cell_type}-TopEffectswithQval.txt",
    # batches = expand(config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/chr{chr}/batches/chr{chr}-batches.done",chr = config["settings"]["chromosomes"], allow_missing = True),
    triplet = expand(config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/snp_genepair_triplets_to_test.txt",chr = config["settings"]["chromosomes"], allow_missing = True)
  output:
    significant_effects = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/EX-TopEffectswithQval-significant.txt",
    genepairs = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/genepairs_to_dump.txt",
    snp_genepairs = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/snp_genepairs_to_dump.txt"
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["get_significant_effects_memory"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["get_significant_effects_memory"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["get_significant_effects_time"]]
  threads: config["get_significant_effects_threads"]
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["singularity_image"],
    script = config["inputs"]["repo_dir"] + "scripts/get_significant_effects.py",
    out = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/",
    batches = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/"
  log: config["outputs"]["output_dir"] + "log/{cell_type}/{cell_type}.get_significant_effects.log"
  shell:
    """
    singularity exec --bind {params.bind} {params.sif} python {params.script} \
      --input {input.coeqtl} \
      --out {params.out} \
      --batches {params.batches} \
      --snp_genepair_triplets {input.triplet}
    """

rule run_coeqtl_all_triplets:
  input:
    annotation = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/annotation.txt.gz",
    correlation = config["inputs"]["co_expr_dir"] + "correlations/data/{cell_type}/chr{chr}/{cell_type}.chr.{chr}.corr.transpose.stripped.txt.gz",
    linkfile = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/linkfile.txt",
    filtered_genotype = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/{cell_type}.eqtlsnps.bgz.vcf.gz",
    gene_pairs = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/genepairs_to_dump.txt",
    triplet_group = config["outputs"]["output_dir"] + "coeQTL/reffiles/{cell_type}/chr{chr}/snp_genepair_triplets_to_test_groups.txt",
    triplet = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/snp_genepairs_to_dump.txt"
  output:
    finished = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/dump/chr{chr}-dump-TopEffects.finished",
    coeqtls = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/dump/chr{chr}-dump-TopEffects.txt"
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["run_coeqtl_dump_memory"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["run_coeqtl_dump_memory"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["run_coeqtl_dump_time"]]
  threads: config["run_coeqtl_dump_threads"]
  log: 
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["singularity_image"],
    threads = 2,
    jar = config["inputs"]["repo_dir"] + "scripts/MbQTL-1.5.1-SNAPSHOT-jar-with-dependencies.jar",
    permutations = 0,
    minobservations = config["mbqtl_settings"]["minobservations"],
    mingenotypecount = config["mbqtl_settings"]["mingenotypecount"],
    out = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/dump/chr{chr}-dump"
  log: config["outputs"]["output_dir"] + "log/{cell_type}/chr{chr}/{cell_type}.chr{chr}.run_coeqtl_dump.log"
  shell:
    """
    mkdir -p $(dirname {params.out})
    singularity exec --bind {params.bind} {params.sif} java -Xmx4g \
      -Djava.util.concurrent.ForkJoinPool.common.parallelism={params.threads} \
      -Dmaximum.threads={params.threads} \
      -Dthread.pool.size={params.threads} \
      -jar {params.jar} \
      -m mbqtl \
      -a {input.annotation} \
      -e {input.correlation} \
      -g {input.linkfile} \
      -v {input.filtered_genotype} \
      --chr {wildcards.chr} \
      --perm {params.permutations} \
      --minobservations {params.minobservations} \
      -gl {input.gene_pairs} \
      -o {params.out} \
      --expgroups {input.triplet_group} \
      --mingenotypecount {params.mingenotypecount} \
      --fisherzmeta \
      --outputall \
      -sgl {input.triplet}
    """

rule merge_triplets:
  input: 
    top_effects = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/{cell_type}-TopEffectswithQval.txt",
    coeqtls = expand(config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/dump/chr{chr}-dump-TopEffects.txt",chr = config["settings"]["chromosomes"], allow_missing=True)
  output: 
    merged = config["outputs"]["output_dir"] + "coeQTL/output/{cell_type}/{cell_type}-TopEffects-allsignificantTriplets.txt"
  resources:
    mem_per_thread_gb = lambda wildcards, attempt: attempt * config["merge_all_triplets_memory"],
    disk_per_thread_gb = lambda wildcards, attempt: attempt * config["merge_all_triplets_memory"],
    time = lambda wildcards,attempt: config["cluster_time"][(attempt - 1) + config["merge_all_triplets_time"]]
  threads: config["merge_all_triplets_threads"]
  params:
    bind = config["inputs"]["bind_path"],
    sif = config["inputs"]["singularity_image"],
    script = config["inputs"]["repo_dir"] + "scripts/filter_significant_triplets.py",
  log: config["outputs"]["output_dir"] + "log/{cell_type}/{cell_type}.merge_all_triplets.log"
  shell:
    """
    singularity exec --bind {params.bind} {params.sif} python {params.script} \
      --significant_egenes {input.top_effects} \
      --input {input.coeqtls} \
      --output {output.merged} > {log} 2>&1
    """